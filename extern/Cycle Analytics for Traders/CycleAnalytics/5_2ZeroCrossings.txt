{
Zero Crossings Period Measurer
© 2013 John F. Ehlers
}
Inputs:
	Period(20),
	Bandwidth(.70);

Vars:
	alpha2(0),
	HP(0),
	gamma1(0),
	alpha1(0),
	beta1(0),
	BP(0),
	Peak(0),
	Real(0),
	counter(0),
	DC(0);

alpha2 = (Cosine(.25*Bandwidth*360 / Period) + Sine(.25*Bandwidth*360 / Period) - 1) / Cosine(.25*Bandwidth*360 / Period);
HP = (1 + alpha2 / 2)*(Close - Close[1]) + (1- alpha2)*HP[1];
beta1 = Cosine(360 / Period);
gamma1 = 1 / Cosine(360*Bandwidth / Period);
alpha1 = gamma1 - SquareRoot(gamma1*gamma1 - 1);
BP = .5*(1 - alpha1)*(HP - HP[2]) + beta1*(1 + alpha1)*BP[1] - alpha1*BP[2];
If Currentbar = 1 or CurrentBar = 2 Then BP = 0;
Peak = .991*Peak;
If AbsValue(BP) > Peak Then Peak = AbsValue(BP);
If Peak <> 0 Then Real = BP / Peak;
DC = DC[1];
If DC < 6 Then DC = 6;
counter = counter + 1;
If Real Crosses Over 0 or Real Crosses Under 0 Then Begin
	DC = 2*counter;
	If 2*counter > 1.25*DC[1] Then DC = 1.25*DC[1];
	If 2*counter < .8*DC[1] Then DC = .8*DC[1];
	counter = 0;
End;

Plot1(DC);
